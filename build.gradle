import org.apache.tools.ant.filters.ReplaceTokens

ext {
    dbfitVersion = '4.0.0'
    isReleaseVersion = !version.endsWith("SNAPSHOT")
    fitSharpVersion = '2.5'
}

task cleanFitnesseroot(type:Exec) {
    def gitArgs = ['clean', '-d', '-x', '-f', 'FitNesseRoot/']
    gitArgs += keepUncommitedTests() ? ['-e', 'FitNesseRoot/DbFit/'] : []

    executable 'git'
    args gitArgs
}

task cleanDist {
    doLast {
        delete 'dist'
        delete 'zips'
        delete 'docs'
    }
}

task copyLocal(dependsOn: [cleanFitnesseroot, cleanDist, ':dbfit-java:assembleAll', ':dbfit-java:libs']) {
    doLast {
        copy {
            from 'LICENSE', 'README.md'
            into 'dist'
        }
        copy {
            from 'bin'
            include 'encrypt.*'
            into 'dist'
        }
        copy {
            from 'templates'
            include 'startFitnesse.*'
            into 'dist'
            rename '(.+)\\.tmpl$', '$1'
            filter(ReplaceTokens,
                tokens: ['dbfitVersion': dbfitVersion, 'fitNesseVersion': project(':dbfit-java').fitNesseVersion])
        }
        copy {
            from 'files'
            into 'dist'
        }
        copy {
            from 'dbfit-java'
            include '**/*.jar'
            exclude 'build/**/*.jar'
            exclude proprietaryLibs()
            exclude experimentalItems()
            into 'dist/lib'
            eachFile {details ->
                details.path = flattenPath(details.path)
            }
            includeEmptyDirs = false
        }
    }
}

task copyExtraLibs(dependsOn: [copyLocal]) {
    doLast {
        copy {
            from 'dbfit-java'
            include proprietaryLibs()
            into 'dist/lib'
            eachFile {details ->
                details.path = flattenPath(details.path)
            }
            includeEmptyDirs = false
        }
        copy {
            from project(':dbfit-java:derby').configurations.derby
            into 'dist/lib'
        }
    }
}

task copyTestLibs(dependsOn: ':dbfit-java:derby:integrationTestJar') {
    doLast {
        copy {
            from 'dbfit-java'
            include '**/*integration-test*.jar'
            exclude 'build/**/*.jar'
            exclude proprietaryLibs()
            exclude experimentalItems()
            into 'dist/lib'
            eachFile { details ->
                details.path = flattenPath(details.path)
            }
            includeEmptyDirs = false
        }
    }
}

task copyConnections(dependsOn: [copyLocal]) {
    doLast {
        subprojects.each { prj ->
            if (prj.hasProperty('connectionPropertiesFiles')) {
                prj.copyConnectionProperties new File(project.rootDir, 'dist')
            }
        }
    }
}

task wikiDocsDir {
    doLast {
        mkdir('docs/Resources/')
    }
}

task fitnesseUpdateList {
    doLast {
        copy {
            from zipTree(fitnesseJar()).matching { include 'Resources/updateList' }
            into 'docs'
            rename 'updateList', 'updateList.fitnesse'
        }
    }
}

task dbfitUpdateList(dependsOn: wikiDocsDir) {
    doLast {
        File f = file('docs/Resources/updateList.dbfit')

        fileTree('FitNesseRoot').each {
            f << "${relativePath(it).toString().replace('\\', '/')}\n"
        }
    }
}

task wikiDocsUpdateList(dependsOn: [fitnesseUpdateList, dbfitUpdateList]) {
    doLast {
        File f = file('docs/Resources/updateList')

        f << file('docs/Resources/updateList.dbfit').text
        f << file('docs/Resources/updateList.fitnesse').text

        delete 'docs/Resources/updateList.fitnesse'
        delete 'docs/Resources/updateList.dbfit'
    }
}

task wikiDocs(dependsOn: [wikiDocsUpdateList]) {
    doLast {
        copy {
            from 'FitNesseRoot'
            into 'docs/Resources/FitNesseRoot'
        }

        ant.jar(destfile: 'dist/lib/dbfit-docs-' + dbfitVersion + '.jar', basedir: 'docs', level: 9)
    }
}

def fitnesseJar() {
    fileTree(dir: 'dist/lib', include: 'fitnesse-*standalone.jar').getSingleFile()
}

def flattenPath(def path) {
    path.replaceAll('.*/([^/]*?\\.jar)', {"${it[1]}"})
}

task bundleFitsharp(dependsOn: copyLocal) {
    doLast {
        def fitSharpFileName = "release.${fitSharpVersion}.net.40.zip"
        mkdir 'dist/fitsharp/'
        ant.get(src: "http://github.com/jediwhale/fitsharp/releases/download/${fitSharpVersion}/${fitSharpFileName}",
                dest: 'dist/fitsharp/',
                verbose: true)
        copy {
            from zipTree("dist/fitsharp/${fitSharpFileName}")
            into 'dist/fitsharp/'
        }
        delete "dist/fitsharp/${fitSharpFileName}"
        file('dist/fitsharp/Runner.exe.config') << '<configuration><runtime><loadFromRemoteSources enabled="true"/></runtime></configuration>'
    }
}

task bundle(type: Zip, dependsOn: [copyLocal, wikiDocs, bundleFitsharp]) {
    description = 'creates the zip file for distributing dbfit'
    from 'dist'
    baseName 'dbfit-complete'
    version dbfitVersion
    destinationDir new File('zips')
}

List cmdLine () {
    onWindows() ? ['cmd', '/c', 'startFitnesse.bat'] : ['./startFitnesse.sh']
}

task start(type: Exec, dependsOn: [copyLocal, copyExtraLibs, copyTestLibs, copyConnections, wikiDocs, bundleFitsharp]) {
    workingDir './dist'
    description = 'starts fitness with dbfit'
    commandLine = cmdLine()
}

task starthere(type: Exec, dependsOn: [copyLocal, copyExtraLibs, copyTestLibs, copyConnections]) {
    workingDir './dist'
    description = 'starts fitness with dbfit on top of real tests sources'
    commandLine = cmdLine() + ['-d', '..', '-o', '-f', 'plugins.properties']
}

task fastbuild(dependsOn: [
    ':dbfit-java:core:check',
    ':dbfit-java:db2:check',
    ':dbfit-java:derby:check',
    ':dbfit-java:hsqldb:check',
    ':dbfit-java:informix:check',
    ':dbfit-java:mysql:check',
    ':dbfit-java:netezza:check',
    ':dbfit-java:oracle:check',
    ':dbfit-java:postgres:check',
    ':dbfit-java:snowflake:check',
    ':dbfit-java:sqlserver:check',
    ':dbfit-java:teradata:check',
    ':dbfit-java:derby:integrationTest',
    ':dbfit-java:hsqldb:integrationTest'
    ])

task cibuild(dependsOn: [
    fastbuild,
    ':dbfit-java:db2:integrationTest',
    ':dbfit-java:mysql:integrationTest',
    ':dbfit-java:oracle:integrationTest',
    ':dbfit-java:postgres:integrationTest'
    ])

task travisbuild(dependsOn: [cibuild])

if (!project.hasProperty('excludeIntegrationTests')) {
    project.ext.set('excludeIntegrationTests', '')
}

Set excludedIntegrationBuildDbTypes() {
    (["dbfit-java", "core"] + project.excludeIntegrationTests.tokenize(",").collect { it.trim() })
}

assert (excludedIntegrationBuildDbTypes() - subprojects.collect { it.name }).empty

def integrationBuildTasks() {
    (subprojects.collect { it.name } - excludedIntegrationBuildDbTypes()).collect {
        ":dbfit-java:${it}:integrationTest"
    }
}

task integrationBuild(dependsOn: [integrationBuildTasks()])

def onWindows() {
    System.properties['os.name'].toLowerCase().contains('windows')
}

def proprietaryLibs() {
    ['**/ojdbc*.jar',
     '**/db2jcc4*.jar',
     '**/terajdbc*.jar',
     '**/tdgssconfig*.jar',
     '**/nzjdbc*.jar',
     '**/sqljdbc*.jar',
     '**/ifxjdbc*.jar'
    ] as Set
}

def experimentalItems() {
    [] as Set
}

def keepUncommitedTests() {
    project.hasProperty('keepTests') && keepTests != 'false'
}

